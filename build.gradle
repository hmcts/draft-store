plugins {
  id 'application'
  id 'jacoco'
  id 'pmd'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.flywaydb.flyway' version '6.4.2'
  id 'org.springframework.boot' version '2.3.0.RELEASE'
  id 'uk.gov.hmcts.java' version '0.5.0'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'org.sonarqube' version '2.8'
  id 'scala'
  id 'com.github.lkishalmi.gatling' version '3.3.0'
}

group 'uk.gov.hmcts.reform'
version '1.1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}


test {
  useJUnitPlatform()
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
  useJUnitPlatform()
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

pmd {
  toolVersion = "6.9.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn test, integration
sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Draft Store"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"

  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all {
      def isNonStable = { String version ->
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
          version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
        }
      }
      if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }
  jcenter()
  mavenCentral()
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  junit: '5.6.2',
  junitPlatform: '1.6.2',
  logging: '5.1.5',
  springBoot: springBoot.class.package.implementationVersion,
  springfoxSwagger: '2.9.2'
]

ext["rest-assured.version"] = '4.0.0'

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group in ['org.apache.logging.log4j']) {
        details.useVersion '2.13.2'
      }
      if (details.requested.group in ['org.scala-lang']) {
        details.useVersion '2.12.10'
      }
      if (details.requested.group in ['org.apache.tomcat.embed']) {
        details.useVersion '9.0.35'
      }
    }
  }
}

dependencies {
  compile(
    "org.springframework.boot:spring-boot-starter-web",
    "org.springframework.boot:spring-boot-starter-actuator",
    "org.springframework.boot:spring-boot-starter-jdbc",
    "org.springframework.boot:spring-boot-starter-validation",
  )
  compile group: 'org.postgresql', name: 'postgresql', version: '42.2.12'

  compile group: 'org.flywaydb', name: 'flyway-core', version: '6.4.2'

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

  compile group: 'com.google.guava', name: 'guava', version: '28.2-jre'

  compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.logging
  compile group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.logging
  compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.logging

  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.2'
  compile group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.13.2'

  compile group: 'uk.gov.hmcts.reform', name: 'reform-api-standards', version: '0.4.0'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.0.4'

  compile group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.5.1'

  testCompile group: 'io.rest-assured', name: 'rest-assured'

  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testCompile group: 'org.junit.vintage', name: 'junit-vintage-engine', version: versions.junit
  testCompile group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform
  testCompile group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testCompile group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
  testCompile 'org.testcontainers:postgresql:1.14.2'

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath

  functionalTestCompile sourceSets.main.runtimeClasspath
  functionalTestCompile sourceSets.test.runtimeClasspath
  functionalTestCompile sourceSets.smokeTest.runtimeClasspath

  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile sourceSets.test.runtimeClasspath

  gatlingCompile 'org.scala-lang:scala-library:2.13.2'
  gatlingCompile 'com.warrenstrange:googleauth:1.5.0'
}

gatling {
  toolVersion '3.3.1'
  scalaVersion '2.12.10'
  simulations = { include "uk.gov.hmcts.reform.draftstore.CreateMultipleDrafts.scala" }
}

mainClassName = 'uk.gov.hmcts.reform.draftstore.DraftStoreApplication'

bootJar {
  archiveName 'draft-store.jar'

  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': project.version
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
